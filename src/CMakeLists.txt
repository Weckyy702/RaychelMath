#HEADER FILES
set(RAYCHELMATH_HEADER_PATH "${RaychelMath_SOURCE_DIR}/include/")
file(GLOB RAYCHELMATH_HEADERS "${RAYCHELMATH_HEADER_PATH}/RaychelMath/*.h")
file(GLOB RAYCHELMATH_IMPL_HEADERS "${RAYCHELMATH_HEADER_PATH}/Impl/*.inl")



#INSTALLATION DIRECTORIES
include(GNUInstallDirs)
set(RAYCHELMATH_INSTALLED_INCLUDE_DIR ${CMAKE_INSTALL_FULL_INCLUDEDIR})

set(RAYCHELMATH_INSTALLED_HEADER_DIR ${RAYCHELMATH_INSTALLED_INCLUDE_DIR}/RaychelMath)
set(RAYCHELMATH_INSTALLED_LIB_DIR ${CMAKE_INSTALL_FULL_LIBDIR}/RaychelMath)


#LIBRARY
add_library(RaychelMath INTERFACE
    ${RAYCHELMATH_HEADERS}
    ${RAYCHELMATH_IMPL_HEADERS}
)

target_include_directories(RaychelMath INTERFACE
    $<BUILD_INTERFACE:${RAYCHELMATH_HEADER_PATH}> # for headers when building
    $<INSTALL_INTERFACE:${RAYCHELMATH_INSTALLED_INCLUDE_DIR}> # for client in install mode
)
target_compile_features(RaychelMath INTERFACE cxx_std_20)



#INSTALLING
install(TARGETS RaychelMath
    EXPORT RaychelMath
)
install(FILES ${RAYCHELMATH_HEADERS}
    DESTINATION ${RAYCHELMATH_INSTALLED_HEADER_DIR}
)
install(FILES ${RAYCHELMATH_IMPL_HEADERS}
    DESTINATION ${RAYCHELMATH_INSTALLED_HEADER_DIR}/Impl
)
install(FILES ../RaychelMathConfig.cmake
    DESTINATION ${RAYCHELMATH_INSTALLED_LIB_DIR}
)
install(EXPORT RaychelMath
    DESTINATION ${RAYCHELMATH_INSTALLED_LIB_DIR}
)